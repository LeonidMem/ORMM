plugins {
    id 'java'
    id 'maven-publish'
}

def ormmGroupId = 'ru.leonidm'
def ormmArtifactId = 'ORMM'
def ormmVersion = '1.4.4'

group ormmGroupId
version ormmVersion

repositories {
    mavenCentral()

    maven { url 'https://mvn.smashup.ru/releases' }
}

sourceCompatibility = 17
targetCompatibility = 17
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
tasks.withType(Javadoc).configureEach { options.encoding = 'UTF-8' }

dependencies {
    implementation 'org.jetbrains:annotations:24.0.1'

    implementation 'ru.leonidm:CommonsM:1.0'

    runtimeOnly 'com.mysql:mysql-connector-j:8.0.32'
    testRuntimeOnly 'com.mysql:mysql-connector-j:8.0.32'
    runtimeOnly 'org.xerial:sqlite-jdbc:3.40.0.0'
    testRuntimeOnly 'org.xerial:sqlite-jdbc:3.40.0.0'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true

        links "https://docs.oracle.com/javase/8/docs/api/"
    }
}

tasks.register('sourceJar', Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
}

tasks.register('packageJavadoc', Jar) {
    archiveClassifier.set("javadoc")
    from javadoc
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = ormmGroupId
            artifactId = ormmArtifactId
            version = ormmVersion

            from components.java
            artifact sourceJar
            artifact packageJavadoc
        }
    }
    repositories {
        maven {
            name = "smashUpRepository"
            url "https://mvn.smashup.ru/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

test {
    useJUnitPlatform()
}
